{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "6e26ed90-93fb-4f14-bb86-f4b92062a0c8",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Running with m=3, q=0.8, T=2.0, c=0.1\n",
      "(3_w,6)-aCMA-ES (mu_w=2.0,w_1=64%) in dimension 2 (seed=3508345769257449819, 2025-08-28T18:27:03.789)\n",
      "  iter   fevals   function value      sigma  axis ratio   time[s]\n",
      "     1        8   -1.49022154e-01   1.01e+00   1.251e+00     3.309\n",
      "     2       16   -1.66890164e-01   9.38e-01   1.298e+00     3.909\n",
      "     3       24   -1.59045460e-01   7.27e-01   1.120e+00     4.662\n",
      "     6       47   -1.60901052e-01   5.85e-01   1.238e+00     6.822\n",
      "     9       71   -1.55862015e-01   1.03e+00   1.492e+00     9.054\n",
      "    12       95   -1.64035483e-01   1.42e+00   2.245e+00    11.170\n",
      "    14      110   -1.58067060e-01   1.14e+00   3.279e+00    12.449\n",
      "eta_opt=2.7579273760467626, kappa_opt=0.05759959176170775, R_bar=0.4525698546957978, info_proc_cap=0.717306248649662\n"
     ]
    }
   ],
   "source": [
    "###############################################################################\n",
    "#      Cognitive capacity and control in the evolution of intelligence \n",
    "#  \n",
    "#   READ:\n",
    "#   Below is the code and setting used for parameter sweep analysis \n",
    "#   This code was run on a high-performance cluster changes \n",
    "#   Change the 'reps' parameter to around 1000 for a quick workstation smoke test\n",
    "#\n",
    "###############################################################################\n",
    "\n",
    "using Random\n",
    "using LinearAlgebra\n",
    "using Base.Threads\n",
    "using Distributions\n",
    "using CMAEvolutionStrategy\n",
    "using DataFrames\n",
    "using CSV\n",
    "\n",
    "###############################################################################\n",
    "# 1. Set parameters\n",
    "###############################################################################\n",
    "m_val = 3 # load or number of items \n",
    "q_val = 0.80 # cue reliability \n",
    "T_val = 2.0 # total time period length \n",
    "c_val = 0.10 # metabolic cost\n",
    "\n",
    "println(\"Running with m=$(m_val), q=$(q_val), T=$(T_val), c=$(c_val)\")\n",
    "\n",
    "###############################################################################\n",
    "# 2. Global settings\n",
    "###############################################################################\n",
    "t_prop = 0.50   # proportion of total time before the cue\n",
    "Δt     = 0.01   # time step for the discrete simulation \n",
    "reps   = 2*10^6 # number of Monte Carlo replicates \n",
    "pop    = 6  # CMA-ES population size \n",
    "gen    = 40 # CMA-ES number of generations \n",
    "\n",
    "###############################################################################\n",
    "# 3. Define the Monte Carlo simulation & expected reward calculation function\n",
    "###############################################################################\n",
    "function expectedR(η::Float64, κ::Float64, m::Int, q::Float64, T::Float64, t_prop::Float64, Δt::Float64, reps::Int)\n",
    "    tspan = 0.0:Δt:T\n",
    "    len_tspan = length(tspan)\n",
    "\n",
    "    x̂₀ = fill(η / m, m)\n",
    "    x̂₁ = clamp.(x̂₀ + log((m - 1) * q / (1 - q)) / m * vcat(m - 1, fill(-1, m - 1)), 0, η)\n",
    "\n",
    "    δ = 1 - exp(-κ * Δt)\n",
    "    σ = sqrt((1 - exp(-2 * κ * Δt)) / (2 * κ))\n",
    "\n",
    "    tq = Int(floor(t_prop * T / Δt)) + 1  # cue-on switch index\n",
    "\n",
    "    function Xtrad_sim(X₀::Vector{Float64}, ϵ::Matrix{Float64})\n",
    "        Xt = copy(X₀)\n",
    "        Xtrad = zeros(len_tspan, m)\n",
    "        Xtrad[1, :] = Xt\n",
    "\n",
    "        # pre-cue drift toward x̂₀ with noise\n",
    "        for t in 2:tq-1\n",
    "            a_Xt = Xt .> 0.0\n",
    "            sum_a_Xt = count(a_Xt)\n",
    "\n",
    "            raw_ΔX = δ * (x̂₀ - Xt) + σ * ϵ[t, :]\n",
    "            ΔX = a_Xt .* raw_ΔX - (dot(a_Xt, raw_ΔX) / sum_a_Xt) * a_Xt\n",
    "\n",
    "            Xt .+= ΔX\n",
    "            Xtrad[t, :] = Xt\n",
    "        end\n",
    "\n",
    "        # post-cue drift toward x̂₁ with noise\n",
    "        for t in tq:len_tspan\n",
    "            a_Xt = Xt .> 0.0\n",
    "            sum_a_Xt = count(a_Xt)\n",
    "\n",
    "            raw_ΔX = δ * (x̂₁ - Xt) + σ * ϵ[t, :]\n",
    "            ΔX = a_Xt .* raw_ΔX - (dot(a_Xt, raw_ΔX) / sum_a_Xt) * a_Xt\n",
    "\n",
    "            Xt .+= ΔX\n",
    "            Xtrad[t, :] = Xt\n",
    "        end\n",
    "\n",
    "        return Xtrad\n",
    "    end\n",
    "\n",
    "    @inline function R(x::AbstractVector{<:Real})\n",
    "        # expected recall over cue types given current allocation x\n",
    "        exp_neg_x = exp.(-x)\n",
    "        return q * (1 - exp_neg_x[1]) +\n",
    "               ((1 - q) / (m - 1)) * sum(1 .- exp_neg_x[2:end])\n",
    "    end\n",
    "\n",
    "    function R_mean(reps::Int)\n",
    "        # expected recall by averaging over time and replicates\n",
    "        R_sum_per_thread = zeros(Float64, nthreads())\n",
    "\n",
    "        @threads for i in 1:reps\n",
    "            X₀ = η * rand(Dirichlet(ones(m)))\n",
    "            ϵ = randn(len_tspan, m)\n",
    "\n",
    "            Xtrad_out = Xtrad_sim(X₀, ϵ)\n",
    "            R_Xt = map(R, eachrow(Xtrad_out))\n",
    "\n",
    "            R_sum_per_thread[threadid()] += sum(R_Xt)\n",
    "        end\n",
    "\n",
    "        return sum(R_sum_per_thread) / (reps * len_tspan)\n",
    "    end\n",
    "\n",
    "    return R_mean(reps)\n",
    "end\n",
    "\n",
    "\n",
    "###############################################################################\n",
    "# 4. Define the objective and run CMA-ES\n",
    "###############################################################################\n",
    "\n",
    "function w_min(params)\n",
    "    η = params[1]\n",
    "    κ = params[2]\n",
    "    return - (expectedR(η, κ, m_val, q_val, T_val, t_prop, Δt, reps) - c_val * (η + κ))\n",
    "end\n",
    "\n",
    "result = minimize(\n",
    "    x -> w_min(x),\n",
    "    [2.00, 0.50],\n",
    "    1.0;\n",
    "    lower = [0.001, 0.001],\n",
    "    upper = [4.00, 1.00],\n",
    "    noise_handling = CMAEvolutionStrategy.NoiseHandling(ασ = 1.1, callback = s -> s > 0),\n",
    "    popsize = pop,\n",
    "    maxiter = gen\n",
    ")\n",
    "\n",
    "final_params = xbest(result)\n",
    "\n",
    "###############################################################################\n",
    "# 5. Outputs \n",
    "###############################################################################\n",
    "\n",
    "eta_opt   = final_params[1]\n",
    "kappa_opt = final_params[2]\n",
    "R_bar     = expectedR(eta_opt, kappa_opt, m_val, q_val, T_val, t_prop, Δt, reps)\n",
    "info_proc_cap = log2(m_val)*R_bar\n",
    "\n",
    "\n",
    "println(\"eta_opt=$(eta_opt), kappa_opt=$(kappa_opt), R_bar=$(R_bar), info_proc_cap=$(info_proc_cap)\")\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "c6f27e8c-07a3-46e5-b5f6-9b5cc5ad0a73",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "noise_RMSD=0.3849649390879382\n"
     ]
    }
   ],
   "source": [
    "###############################################################################\n",
    "# 6. Noise (requires previous cell run) \n",
    "###############################################################################\n",
    "\n",
    "function noise(η::Float64, κ::Float64, m::Int, q::Float64, T::Float64,\n",
    "               t_prop::Float64, Δt::Float64, reps::Int)\n",
    "    tspan = 0.0:Δt:T\n",
    "    L = length(tspan)\n",
    "\n",
    "    x̂₀ = fill(η / m, m)\n",
    "    x̂₁ = clamp.(x̂₀ .+ log((m - 1) * q / (1 - q)) / m .* vcat(m - 1, fill(-1, m - 1)), 0, η)\n",
    "\n",
    "    δ = 1 - exp(-κ * Δt)\n",
    "    σ = sqrt((1 - exp(-2 * κ * Δt)) / (2 * κ))\n",
    "    tq = Int(floor(t_prop * T / Δt)) + 1  \n",
    "\n",
    "    dsum_per_thread = zeros(L, nthreads())\n",
    "\n",
    "    @threads for rep in 1:reps\n",
    "        X₀ = η * rand(Dirichlet(ones(m)))\n",
    "        ϵ  = randn(L, m)\n",
    "\n",
    "        Xt_c = copy(X₀)\n",
    "        Xt_n = copy(X₀)\n",
    "\n",
    "        dloc = zeros(L)\n",
    "        dloc[1] = norm(Xt_n .- Xt_c)^2\n",
    "\n",
    "        for t in 2:(tq-1)\n",
    "            a_c = Xt_c .> 0; sc = count(a_c)   \n",
    "            a_n = Xt_n .> 0; sn = count(a_n)   \n",
    "\n",
    "            raw_c = δ * (x̂₀ - Xt_c)\n",
    "            raw_n = σ * ϵ[t, :]\n",
    "\n",
    "            Δc = a_c .* raw_c .- (dot(a_c, raw_c) / sc) .* a_c\n",
    "            Δn = a_n .* raw_n .- (dot(a_n, raw_n) / sn) .* a_n\n",
    "\n",
    "            Xt_c .+= Δc\n",
    "            Xt_n .+= Δn\n",
    "\n",
    "            dloc[t] = norm(Xt_n .- Xt_c)^2\n",
    "        end\n",
    "\n",
    "        for t in tq:L\n",
    "            a_c = Xt_c .> 0; sc = count(a_c)\n",
    "            a_n = Xt_n .> 0; sn = count(a_n)\n",
    "\n",
    "            raw_c = δ * (x̂₁ - Xt_c)\n",
    "            raw_n = σ * ϵ[t, :]\n",
    "\n",
    "            Δc = a_c .* raw_c .- (dot(a_c, raw_c) / sc) .* a_c\n",
    "            Δn = a_n .* raw_n .- (dot(a_n, raw_n) / sn) .* a_n\n",
    "\n",
    "            Xt_c .+= Δc\n",
    "            Xt_n .+= Δn\n",
    "\n",
    "            dloc[t] = norm(Xt_n .- Xt_c)^2\n",
    "        end\n",
    "\n",
    "        dsum_per_thread[:, threadid()] .+= dloc\n",
    "    end\n",
    "\n",
    "    dsum = vec(sum(dsum_per_thread, dims=2))\n",
    "    rms_vec = sqrt.(dsum ./ reps) ./ η    \n",
    "    return sum(rms_vec) / L              \n",
    "end\n",
    "\n",
    "noise_RMSD = noise(eta_opt, kappa_opt, m_val, q_val, T_val, t_prop, Δt, reps)\n",
    "println(\"noise_RMSD=$(noise_RMSD)\")\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0103f7ad-64f9-484f-b59f-841fe52ddd2c",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia (15 threads) 1.11.3",
   "language": "julia",
   "name": "julia-_15-threads_-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
